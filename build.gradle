apply plugin: 'java'

subprojects {
    if (name == 'museum-scripts') return
    apply plugin: 'java'

    targetCompatibility = sourceCompatibility = '1.8'

    repositories {
        // MavenLocal is considered to be a bad practice
        // mavenLocal()
        mavenCentral()
        maven {
            url "https://repo.implario.dev/public"
        }
    }

    def nmsEnv = System.getenv('NMS_MAVEN_URI')
    def cristalixEnv = System.getenv('CRISTALIX_MAVEN_URI')
    def nms = nmsEnv ? nmsEnv : "$nmsMavenUri"
    def cristalix = cristalixEnv ? cristalixEnv : "$cristalixMavenUri"

    project.ext.mavenUri = nms + ' ' + cristalix
    apply from: '../mavenAuth.gradle'

    compileJava.options.encoding 'UTF-8'

    tasks.withType(JavaCompile) {
        options.encoding 'UTF-8'
    }

    dependencies {
        annotationProcessor 'org.projectlombok:lombok:1.18.12'
        compileOnly 'org.projectlombok:lombok:1.18.12'
        compileOnly 'io.netty:netty-all:4.1.50.Final'
        compileOnly 'com.google.guava:guava:29.0-jre'
        implementation 'com.google.code.gson:gson:2.8.6'
        implementation 'clepto:clepto-java:0.3.9'
    }

    jar {
        from sourceSets.main.resources
        from {
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
            configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        }
    }

    task deploy(type: Copy) {
        dependsOn build
        from jar
        String dirPath = System.getenv()["OUTPUT_DIR"]
        File file = dirPath ? file(dirPath) : rootProject.buildDir
        into file
        doLast {
            println "Deployed $jar.archiveName to $file.path"
        }
    }

}


