plugins {
    id 'com.github.node-gradle.node' version '2.2.4'
    id 'de.undercouch.download' version '1.2'
}

import de.undercouch.gradle.tasks.download.Download

task downloadScripts(type: Download) {
    def scriptFile = new File(projectDir, 'src/holotables.ts')
    onlyIf { !scriptFile.exists() }
    quiet true
    src 'https://raw.githubusercontent.com/DelfikPro/cristalix-client-api/stable/examples/holotables.ts'
    dest scriptFile
}

task npmInit(type: NpmTask) {
    args = ['i']
}

task compileScripts(type: NpxTask) {

    dependsOn npmInit
    dependsOn downloadScripts

    command = '@cristalix/bundler'
    args = ['src']

    inputs.dir(file('src'))
            .withPropertyName('src')
            .withPathSensitivity(PathSensitivity.RELATIVE)

    inputs.files('package.json', 'package-lock.json')
            .withPropertyName('configFiles')
            .withPathSensitivity(PathSensitivity.RELATIVE)

    outputs.dir(buildDir)
            .withPropertyName('bundle')

}
